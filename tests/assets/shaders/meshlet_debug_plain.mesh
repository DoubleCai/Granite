#version 450
#extension GL_EXT_mesh_shader : require
#extension GL_EXT_shader_explicit_arithmetic_types_int16 : require
#extension GL_EXT_shader_explicit_arithmetic_types_int8 : require
#extension GL_EXT_scalar_block_layout : require

#extension GL_KHR_shader_subgroup_basic : require
#if defined(MESHLET_PRIMITIVE_CULL_WAVE32) && MESHLET_PRIMITIVE_CULL_WAVE32
#extension GL_KHR_shader_subgroup_shuffle : require
#extension GL_KHR_shader_subgroup_ballot : require
#extension GL_KHR_shader_subgroup_arithmetic : require
#endif

#ifndef MESHLET_SIZE
#error "Must define MESHLET_SIZE"
#endif

layout(max_primitives = MESHLET_SIZE, max_vertices = MESHLET_SIZE, triangles) out;
layout(local_size_x = 32, local_size_y_id = 0) in;

#include "meshlet_render_types.h"
#include "meshlet_primitive_cull.h"

layout(location = 0) out vec3 vNormal[];
layout(location = 1) out vec4 vTangent[];
layout(location = 2) out vec2 vUV[];
layout(location = 3) perprimitiveEXT out uint vDrawID[];

layout(set = 1, binding = 0) uniform UBO
{
    mat4 VP;
};

layout(set = 1, binding = 2) uniform UBOViewport
{
    vec4 viewport;
};

layout(set = 0, binding = 0, scalar) readonly buffer IBO
{
    u8vec3 data[];
} ibo;

layout(set = 0, binding = 1, scalar) readonly buffer VBOPOS
{
    vec3 data[];
} pos;

struct TexturedAttr
{
    uint n;
    uint t;
    vec2 uv;
};

layout(set = 0, binding = 2, std430) readonly buffer VBOATTR
{
    TexturedAttr data[];
} attr;

layout(set = 0, binding = 3, std430) readonly buffer IndirectCommands
{
    IndirectDrawMesh draws[];
} indirect_commands_mesh;

#ifdef MESHLET_RENDER_TASK_HIERARCHICAL
taskPayloadSharedEXT CompactedDrawInfoPayload mesh_payload;
#else
layout(set = 0, binding = 4, std430) readonly buffer CompactedDraws
{
    CompactedDrawInfo infos[];
} mesh_payload;
#endif

layout(set = 0, binding = 5, std430) readonly buffer Transforms
{
    mat4 data[];
} transforms;

layout(set = 0, binding = 10) buffer Stats
{
    uint invocations;
    uint prim;
    uint vert;
} stats;

mediump vec4 unpack_bgr10a2(uint v)
{
    mediump ivec4 vs;
    vs.x = bitfieldExtract(int(v), 0, 10);
    vs.y = bitfieldExtract(int(v), 10, 10);
    vs.z = bitfieldExtract(int(v), 20, 10);
    vs.w = bitfieldExtract(int(v), 30, 3);
    return vec4(vs) / vec4(511.0, 511.0, 511.0, 1.0);
}

void main()
{
    uint compacted_meshlet_index = meshlet_get_meshlet_index();
    uint base_chunk_index = meshlet_get_base_chunk_index();

#if defined(MESHLET_RENDER_TASK_HIERARCHICAL) && !MESHLET_RENDER_TASK_HIERARCHICAL
    CompactedDrawInfo task = mesh_payload.info;
    task.meshlet_index += uint(mesh_payload.offsets[compacted_meshlet_index]);
#else
    CompactedDrawInfo task = mesh_payload.infos[compacted_meshlet_index];
#endif

#if defined(MESHLET_PRIMITIVE_CULL_WAVE32) && MESHLET_PRIMITIVE_CULL_WAVE32
    uint linear_index = gl_SubgroupID * gl_SubgroupSize + gl_SubgroupInvocationID;
#else
    uint linear_index = gl_LocalInvocationIndex;
#endif

    IndirectDrawMesh meshlet = indirect_commands_mesh.draws[task.meshlet_index];
    mat4 M = transforms.data[task.node_offset];

    // Transform positions.
    vec3 pos = pos.data[meshlet.vertex_offset + linear_index + 32u * base_chunk_index];
    vec3 world_pos = (M * vec4(pos, 1.0)).xyz;
    vec4 clip_pos = VP * vec4(world_pos, 1.0);

    meshlet_setup_local_invocation(gl_LocalInvocationID.xy);

    uvec3 prim = uvec3(ibo.data[meshlet.primitive_offset + linear_index + 32u * base_chunk_index]);
    meshlet_emit_primitive(prim, clip_pos, viewport);

    if (linear_index < shared_active_prim_count_total)
        vDrawID[linear_index] = task.meshlet_index;

    if (meshlet_lane_has_active_vert())
    {
        uint out_vert_index = meshlet_compacted_vertex_output();
        gl_MeshVerticesEXT[out_vert_index].gl_Position = clip_pos;

        TexturedAttr a = attr.data[meshlet.vertex_offset + linear_index + 32u * base_chunk_index];

        mediump vec3 n = unpack_bgr10a2(a.n).xyz;
        mediump vec4 t = unpack_bgr10a2(a.t);
        vUV[out_vert_index] = a.uv;
        vNormal[out_vert_index] = mat3(M) * n;
        vTangent[out_vert_index] = vec4(mat3(M) * t.xyz, t.w);
    }

    if (gl_LocalInvocationIndex == 0)
    {
        atomicAdd(stats.invocations, gl_WorkGroupSize.x * gl_WorkGroupSize.y);
        atomicAdd(stats.prim, shared_active_prim_count_total);
        atomicAdd(stats.vert, shared_active_vert_count_total);
    }
}
